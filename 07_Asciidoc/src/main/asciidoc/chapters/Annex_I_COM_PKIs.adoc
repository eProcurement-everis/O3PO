== Annex I: Public Procurement PKIs

This section provides some more examples of SPARQL queries and results obtained
for the period January 2018 to May 2018.

The queries are intended to produce the same
link:http://ec.europa.eu/internal_market/scoreboard/performance_per_policy_area/public_procurement/index_en.htm[PKIs]
as the ones shown in the EU Single Market on Public Procurement.

=== Indicator I: Single bidder

It measures the proportion of contracts awarded where there was just a single bidder
(only Contract Notices under Directive 2014/24/EU and excluding framework agreements,
as they have different reporting patterns).


.Indicator I: Single bidder
[code]
----
PREFIX : <http://data.europa.eu/ePO/ontology#>
PREFIX rov: <http://www.w3.org/ns/regorg#>
PREFIX org: <http://www.w3.org/ns/org#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX vcard: <http://www.w3.org/2006/vcard/ns#>
select distinct ?c (concat(str(round(?cc * 100 / ?countContract)), '%') as ?percent) where
{
    {select ?c (count(?c) as ?cc) where {
        {select ?ar (count(?w) as ?cw) where{
            ?contract a :Contract ;
                      :refersToAwardResult ?ar.
            ?ar :hasWinner ?w
            filter not exists {?contract :hasFrameworkContract ?fc}

            } group by ?ar
            having (count(?w) = 1)}
        ?ar :hasWinner ?w .
           ?w org:hasSite ?s .
        ?s vcard:hasAddress ?a .
        ?a vcard:country-name ?c} group by ?c order by ?c
    }
    {
        select ?c (count(?c) as ?countContract) where
        {
            ?con a :Contract ;
               :refersToAwardResult ?ar .
            ?ar :hasWinner ?w .
            ?w org:hasSite ?s .
            ?s vcard:hasAddress ?add .
            ?add vcard:country-name ?c
        } group by ?c order by ?c
    }
}
----

.KPI I: Single Bidder
image::KPI_1_EU.png[KPI I, align="center"]

