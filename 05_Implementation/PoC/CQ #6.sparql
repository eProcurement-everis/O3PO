########## Competency Question CQ#6 ##########

### Use Case #1: Transparency and Monitoring
### Use Case #4: Analyzing eProcurement procedures

### Competency Question description: For a given period of time, percentage of contracts awarded segmented by contract nature (percentage of works, percentage of services, percentage of supplies, etc.); 

################################################

PREFIX : <http://data.europa.eu/ePO/ontology#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
select ?ContractNatureCodeType((?cn_type* 100 / ?all_awarded_pp)  as ?Percentage) 
where {
    {
        select (count(DISTINCT ?awarded_pp)as ?all_awarded_pp) 
        where {
            ?awarded_pp a :ProcurementProcedure ;
                        :includesLot ?awarded_lots.
            ?awarded_contract a :Contract;
              :refersToLot ?awarded_lots;
        	  :hasContractPurpose ?awarded_Purpose;
              :refersToAwardResult ?awarded_ap.
            ?awarded_ap a :AwardResult ;
                        :hasAwardResultDateOfConclusion ?awarded_YEAR .
            ?awarded_Purpose :hasContractNatureCodeType ?ContractNatureCodeType.
            FILTER
            (?awarded_YEAR >= "2010-01-01"^^xsd:dateTime &&
                ?awarded_YEAR <= "2018-12-31"^^xsd:dateTime).
        } 
    }
    {
        select ((count (DISTINCT ?pp)) as ?cn_type) ?ContractNatureCodeType
        where {  
            ?pp a :ProcurementProcedure ;
                :includesLot ?lots.
            ?contract a :Contract;
              :refersToLot ?lots;
              :hasContractPurpose ?purpose;
              :refersToAwardResult ?ap.
            ?ap a :AwardResult ;
                :hasAwardResultDateOfConclusion ?YEAR .
            ?purpose :hasContractNatureCodeType ?ContractNatureCodeType.
            FILTER
            (?YEAR >= "2010-01-01"^^xsd:dateTime &&
                ?YEAR <= "2018-12-31"^^xsd:dateTime).
        } group by ?ContractNatureCodeType
    }
}

################################################