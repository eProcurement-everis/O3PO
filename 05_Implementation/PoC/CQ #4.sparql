######### QUERY #4 METADATA #####################

### USE CASE #4: Analyzing eProcurement procedures

### User Story: As a Contracting Authority, I want to know the percentage of contracts awarded segmented by procurement procedure, so I can prepare the annual report.

### Competency Question: 4.	For a given period of time, indicate the percentages of type of procedures for the awarded procedures. (Technique has been removed from the model).

#################################################

PREFIX : <http://data.europa.eu/ePO/ontology#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
select ?ProcurementProcedureType((?pp_type* 100 / ?all_awarded_pp)  as ?Percentage) 
where { 
    {   
        select (count(DISTINCT ?awarded_pp)as ?all_awarded_pp) 
        where {
            ?awarded_pp a :ProcurementProcedure ;
                        :includesLot ?awarded_lots.
            optional{ ?awarded_pp :hasProcurementProcedureType ?awarded_ProcurementProcedureCodeType.}
            ?awarded_contract a :Contract;
              :refersToLot ?awarded_lots;
              :refersToAwardResult ?awarded_ap.
            ?awarded_ap a :AwardResult ;
                        :hasAwardResultDateOfConclusion ?awarded_YEAR .
            FILTER
            (?awarded_YEAR >= "2010-01-01"^^xsd:dateTime &&
                ?awarded_YEAR <= "2018-12-31"^^xsd:dateTime).
        } 
    }
    {
        select ((count (DISTINCT ?pp)) as ?pp_type) ?ProcurementProcedureType
        where {  
            ?pp a :ProcurementProcedure ;
                :includesLot ?lots.
            optional{ ?pp :hasProcurementProcedureType ?ProcurementProcedureType.}
            ?contract a :Contract;
              :refersToLot ?lots;
              :refersToAwardResult ?ap.
            ?ap a :AwardResult ;
                :hasAwardResultDateOfConclusion ?YEAR .
            FILTER
            (?YEAR >= "2010-01-01"^^xsd:dateTime &&
                ?YEAR <= "2018-12-31"^^xsd:dateTime).
        } group by ?ProcurementProcedureType
    }
}

#################################################